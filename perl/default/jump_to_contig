#!/usr/local/bin/perl
package anchorview;

# Form-processing script that takes parameters from mapview
# and passes the appropriate URL to contigview or cytoview
# (or throws an error back to mapview!)

use strict;
use warnings;
no warnings "uninitialized";

use CGI;
use URI::Escape qw(uri_escape);

use EnsEMBL::Web::CoreObjects;
use EnsEMBL::Web::DBSQL::DBConnection;
use EnsEMBL::Web::Proxy::Factory;
use EnsEMBL::Web::RegObj;

my $input         = new CGI;
my $species       = $ENV{'ENSEMBL_SPECIES'};
my $species_defs  = $ENSEMBL_WEB_REGISTRY->species_defs;
my $species_path  = $species_defs->species_path;
my $db_connection = $species ne 'common' ? new EnsEMBL::Web::DBSQL::DBConnection($species, $species_defs) : undef;
my $core_objects  = new EnsEMBL::Web::CoreObjects($input, $db_connection);
my $factory       = new EnsEMBL::Web::Proxy::Factory('Location', {
  _input        => $input,
  _core_objects => $core_objects,
  _databases    => $db_connection
});

eval {
  $factory->createObjects;
};

my $extra_url;

foreach (qw( type1 anchor1 type2 anchor2 downstream upstream chr )) {
  $extra_url .= sprintf ';%s=%s', $_, uri_escape($factory->param($_)) if $factory->param($_);
}

if ($factory->has_problem_type('fatal')) {
  $factory->redirect("$species_path/mapview?error=1$extra_url");
} elsif ($factory->has_a_problem) {
  $factory->redirect("$species_path/mapview?error=2$extra_url"); ## this means not all on same chromosome
} else { ## grab the object and check to see if it is less than 5Mb
  my $object = $factory->object; 
  my $script = $object->length < 1.0001e6 * ($species_defs->ENSEMBL_GENOME_SIZE || 1) ? 'contigview' : 'cytoview';
     $script = 'cytoview' if $species_defs->NO_SEQUENCE;
     
  my $url = sprintf(
    '%s/%s?l=%s:%d-%s',
    $species_path,
    $script,
    $object->seq_region_name,
    $object->seq_region_start,
    $object->seq_region_end
  );
  
  $url .= ';h=' . join('|', 
    ($object->param('type1') eq 'bp' ? () : $object->param('anchor1')),
    ($object->param('type2') eq 'bp' ? () : $object->param('anchor2'))
  );
  
  $factory->redirect($url);
}

1;
